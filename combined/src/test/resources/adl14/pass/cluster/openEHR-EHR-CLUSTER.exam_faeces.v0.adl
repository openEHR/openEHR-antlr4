archetype (adl_version=1.4; uid=ae1161d1-05e2-4826-8a0e-e7dc523626f0)
	openEHR-EHR-CLUSTER.exam_faeces.v0

concept
	[at0000]	-- Examination of faeces
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-11-16">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the findings observed during the examination of faeces.">
			use = <"Use to record a narrative description, structured details and clinical interpretation about the findings observed during examination of faeces, which could occur during routine clinical observations or as part of a physical examination. 

This archetype has been designed to be used within:
- the 'Examination findings' SLOT in the OBSERVATION.exam and related CLUSTER archetypes; 
- the OBSERVATION.faecal_output to describe the diarrhoea being measured; or
- the OBSERVATION.laboratory_test to describe physical properties about a faecal sample/specimen. 

It can also be used within other OBSERVATION or CLUSTER archetypes, where clinically appropriate.

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			keywords = <"faeces", "poo">
			misuse = <"Not to be used for recording measurements of faecal output - use OBSERVATION.faecal_output for this purpose.

Not to be used for recording the clinical history about bowel habits - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Nasjonal IKT HF, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway", "Heather Leslie, Atomica Informatics, Australia">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"C693920AD0ABB8AEA17983C7564BBB17">
		["build_uid"] = <"581800da-1b22-43ba-82af-1110c9969676">
		["revision"] = <"0.0.2-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of faeces
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {	-- Colour
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0013] occurrences matches {0..*} matches {	-- Blood presence
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0014] occurrences matches {0..1} matches {	-- Consistency
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Examination findings
				include
					archetype_id/value matches {/.*/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of faeces">
					description = <"Findings observed during the examination of faeces.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the examination of faeces.">
				>
				["at0004"] = <
					text = <"Examination findings">
					description = <"Structured details about the examination findings of the faeces.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the physical examination findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the physical examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the examination of faeces, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0012"] = <
					text = <"Colour">
					description = <"The overall colour of the faeces.">
				>
				["at0013"] = <
					text = <"Blood presence">
					description = <"Narrative description about findings related to blood in or around the faecal specimen.">
				>
				["at0014"] = <
					text = <"Consistency">
					description = <"Narrative description about the firmness of the faeces.">
				>
			>
		>
	>
