archetype (adl_version=1.4; uid=4946e311-71c7-41d0-963e-ea8eaa3f0a2e)
	openEHR-EHR-CLUSTER.operative_procedure.v0

concept
	[at0000]	-- Operative procedure
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Nyree Taylor">
		["organisation"] = <"Ocean Health Systems, Australia">
		["email"] = <"nyree.taylor@oceanhealthsystems.com">
		["date"] = <"2017-07-26">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the operative aspects of a procedure.">
			use = <"Use to record details about the operative aspects of any surgical procedure. 

This archetype is designed to be nested within the 'Procedure detail' SLOT within the ACTION.procedure archetype or similar, which will identifiy the name of the procedure and information that is common to all types of procedures.

Other CLUSTER archetypes can be inserted into the SLOTs to extend this generic archetype to capture further details for the identified operation - for example: CLUSTER.surgical_preparation; CLUSTER.myringoplasty and CLUSTER.closure.">
			keywords = <"procedure", "surgery", "incision", "resection", "intervention", "surgical", "trauma", "repair">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Heather Leslie, Ocean Health Systems, Australia", "Graeme Crossland, HHIMS program, Northern Territory, Australia">
	other_details = <
		["licence"] = <"  This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Nyree Taylor, Ocean Health Systems, Australia<nyree.taylor@oceanhealthsystems.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"0B61FC138DCD04219DC8E97AB49A636F">
		["build_uid"] = <"31822ca5-96dd-4ff3-9d85-220b450db65a">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Operative procedure
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Episode
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0002, 	-- Primary
							at0003]	-- Revision
						}
					}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Pre-operative assessment
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.asa_status\.v0/}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {	-- Approach
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0007] occurrences matches {0..*} matches {	-- Operation details
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0009] occurrences matches {0..1} matches {	-- Closure
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0010] occurrences matches {0..*} matches {	-- Closure details
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0011] occurrences matches {0..1} matches {	-- Operative diagnosis
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0013] occurrences matches {0..1} matches {	-- Outcomes
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Operative procedure">
					description = <"Details about the operative aspects of a procedure.">
				>
				["at0001"] = <
					text = <"Episode">
					description = <"Sequence order of the procedure.">
				>
				["at0002"] = <
					text = <"Primary">
					description = <"The first time the procedure has been performed on this body site.">
				>
				["at0003"] = <
					text = <"Revision">
					description = <"Subsequent times the procedure is performed.">
				>
				["at0005"] = <
					text = <"Pre-operative assessment">
					description = <"Structured details about pre-procedure activities.">
				>
				["at0006"] = <
					text = <"Approach">
					description = <"Description about the surgical technique.">
				>
				["at0007"] = <
					text = <"Operation details">
					description = <"Specific details about the operation.">
				>
				["at0009"] = <
					text = <"Closure">
					description = <"Narrative description about the closure of the wound.">
				>
				["at0010"] = <
					text = <"Closure details">
					description = <"Specific details about the closure of the wound.">
				>
				["at0011"] = <
					text = <"Operative diagnosis">
					description = <"Single word, phrase or brief description representing the clinical findings from the operation.">
				>
				["at0012"] = <
					text = <"Comment">
					description = <"Additional narrative about the operative procedure, not captured in other fields.">
				>
				["at0013"] = <
					text = <"Outcomes">
					description = <"Narrative description of the result or consequences of the operation or procedure.">
				>
			>
		>
	>
