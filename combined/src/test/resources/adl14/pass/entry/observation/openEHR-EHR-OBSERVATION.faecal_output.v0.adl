archetype (adl_version=1.4; uid=92e3730d-1ab5-4759-b9f5-7aa50803cdd9)
	openEHR-EHR-OBSERVATION.faecal_output.v0

concept
	[at0000]	-- Faecal output
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-10-09">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about faecal output.">
			use = <"Use to record details about faecal output.

This archetype can be used to record details about:
- each single bowel motion, including episodes of diarrhoea; or
- the total faecal output over a specified period of time, for example the amount of diarrhoea as art of a fluid balance chart over an 8 hour nursing shift.

Each measurement should be recorded using a separate instance of this archetype.

If an individual has diarrhoea, a fluid balance chart may require inclusion of this archetype alongside multiple instances of OBSERVATION.fluid_output and OBSERVATION.fluid_input archetype to capture all of the various inputs and outputs, plus a single instance of the OBSERVATION.fluid_balance to record the overall fluid status.">
			keywords = <"fluid", "balance", "output", "urine", "vomit", "drain", "drainage", "aspirate", "exudate", "loss">
			misuse = <"Not to be used to record fluid input. Use OBSERVATION.fluid_input for this purpose.

Not to be used to record fluid output, other than diarrhoea. Use OBSERVATION.fluid_output for this purpose.

Not to be used to record fluid balance calculations or to record the calculation of insensible fluid loss. Use OBSERVATION.fluid_balance for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Fluid output, Published archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2018-11-16]. Available from: https://ckm.openehr.org/ckm/#showArchetype_1013.1.1683">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"82041513A9176E7BA74B8338E79C0E98">
		["build_uid"] = <"3e71d645-d655-457f-ab2a-425a3d9a8d38">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Faecal output
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- List
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- Amount
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::129]>
												list = <
													["1"] = <
														units = <"ml">
														magnitude = <|>=0.0|>
														precision = <|0|>
													>
													["2"] = <
														units = <"g">
														magnitude = <|>=0.0|>
														precision = <|0|>
													>
												>
											>
										}
									}
									allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- Faecal details
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_faeces\.v0/}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {    -- List
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0009] occurrences matches {0..1} matches {    -- Method
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0010,    -- Estimated
									at0011]    -- Measured
								}
							}
						}
					}
					allow_archetype CLUSTER[at0012] occurrences matches {0..1} matches {    -- Output device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0013] occurrences matches {0..1} matches {    -- Measurement device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Faecal output">
					description = <"The measurement of faeces excreted from the body.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Amount">
					description = <"The amount of faeces excreted.">
					comment = <"If absorbant material, such as diapers and dressings, are weighed as part of managing faecal output, the weight will need to be converted to a volume or mass for recording.">
				>
				["at0005"] = <
					text = <"Faecal details">
					description = <"Additional details about the faeces, including macroscopic appearance or other tests not currently captured in the structured data.">
					comment = <"For example: the colour of the faeces.">
				>
				["at0006"] = <
					text = <"Comment">
					description = <"Additional narrative about the faecal output not captured in other fields.">
				>
				["at0008"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Method">
					description = <"The approach used to quantify the amount of faeces.">
				>
				["at0010"] = <
					text = <"Estimated">
					description = <"The faecal ourput has been estimated.">
				>
				["at0011"] = <
					text = <"Measured">
					description = <"The faecal output has been directly measured.">
				>
				["at0012"] = <
					text = <"Output device">
					description = <"Details of the device that was used to collect the faeces.">
					comment = <"*">
				>
				["at0013"] = <
					text = <"Measurement device">
					description = <"Details of the device used to measure the faecal output.">
					comment = <"*">
				>
				["at0014"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"e.g. Local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
			>
		>
	>
